/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a = x, b = false, sel = zx, out = zerox);
    Not16(in = zerox, out = notzerox);
    Mux16(a = zerox, b = notzerox, sel = nx, out = znx);

    Mux16(a = y, b = false, sel = zy, out = zeroy);
    Not16(in = zeroy, out = notzeroy);
    Mux16(a = zeroy, b = notzeroy, sel = ny, out = zny);

    Add16(a = znx, b = zny, out = xaddy);
    And16(a = znx, b = zny, out = xandy);
    Mux16(a = xandy, b = xaddy, sel = f, out = fxy);
    Not16(in = fxy, out = nfxy);
    Mux16(a = fxy, b = nfxy, sel = no, out = innerout, out = out);

    // zr
    Or16(a = innerout, b = false, out[0..7] = out0to8, out[8..15] = out8t015);
    Or8Way(in = out0to8, out = o1);
    Or8Way(in = out8t015, out = o2);
    Or(a = o1, b = o2, out = o1o2);
    Not(in = o1o2, out = zr);

    // ng
    // n bits number: whenever the nth bit is set, we're dealing with a negative number
    Or16(a = innerout, b = false, out[0..14] = drop, out[15] = o3);
    And(a = o3, b = true, out = ng);
}
