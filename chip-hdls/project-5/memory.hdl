/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid and reads 0. Access to any address
 * in the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the Screen
 * and Keyboard chip specifications given in the lectures and the book.
 */
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    // 0x0000 - 0x3fff -> RAM16K
    // 0x4000 - 0x5fff -> Screen
    // 0x6000 -> Keyboard
    PARTS:
    DMux8Way(
        in=load, sel=address[12..14],
        a=l0, b=l1, c=l2, d=l3, e=l4, f=l5, g=l6, h=l7
    );

    Or(a=l0, b=l1, out=l0l1);
    Or(a=l0l1, b=l2, out=l0l1l2);
    Or(a=l0l1l2, b=l3, out=loadRam);
    RAM16K(
        in=in, load=loadRam, address=address[0..13],
        out=ramOut
    );

    Or(a=l4, b=l5, out=loadScreen);
    Screen(
        in=in, load=loadScreen, address=address[0..12],
        out=screenOut
    );

    Keyboard(out=keyOut);

    Mux8Way16(a=ramOut, b=ramOut, c=ramOut, d=ramOut,
        e=screenOut, f=screenOut, g=keyOut, h=false,
        sel=address[12..14], out=out);
}
