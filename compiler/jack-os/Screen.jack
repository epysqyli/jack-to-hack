class Screen {
	/* The screen memory map goes from 16384 to 24576 */
	static Array screen;

	/* true: black, false: white */
	static boolean color;
	static Array squaresUpToSixteen;
	static Array xs, ys;

	function void init() {
		var int i, xsAddr;

		let screen = 16384;
		let color = true;

		let squaresUpToSixteen = Array.new(16);
		let squaresUpToSixteen[0] = 1;
		let squaresUpToSixteen[1] = 2;
		let squaresUpToSixteen[2] = 4;
		let squaresUpToSixteen[3] = 8;
		let squaresUpToSixteen[4] = 16;
		let squaresUpToSixteen[5] = 32;
		let squaresUpToSixteen[6] = 64;
		let squaresUpToSixteen[7] = 128;
		let squaresUpToSixteen[8] = 256;
		let squaresUpToSixteen[9] = 512;
		let squaresUpToSixteen[10] = 1024;
		let squaresUpToSixteen[11] = 2048;
		let squaresUpToSixteen[12] = 4096;
		let squaresUpToSixteen[13] = 8192;   // 0010000000000000
		let squaresUpToSixteen[14] = 16384;  // 0100000000000000
		let squaresUpToSixteen[15] = ~32767; // 1000000000000000

		let ys = Array.new(256);
		let i = 0;
		while (i < 256) {
			let ys[i] = screen + (i * 32);
			let i = i + 1;
		}

		let xs = Array.new(512);
		let i = 0;
		let xsAddr = 0;

		while (i < 32) {
			let xs[xsAddr] = i;
			let xs[xsAddr + 1] = i;
			let xs[xsAddr + 2] = i;
			let xs[xsAddr + 3] = i;
			let xs[xsAddr + 4] = i;
			let xs[xsAddr + 5] = i;
			let xs[xsAddr + 6] = i;
			let xs[xsAddr + 7] = i;
			let xs[xsAddr + 8] = i;
			let xs[xsAddr + 9] = i;
			let xs[xsAddr + 10] = i;
			let xs[xsAddr + 11] = i;
			let xs[xsAddr + 12] = i;
			let xs[xsAddr + 13] = i;
			let xs[xsAddr + 14] = i;
			let xs[xsAddr + 15] = i;

			let xsAddr = xsAddr + 16;
			let i = i + 1;
		}

		return;
	}

	function void clearScreen() {
		var int currentAddress;
		let currentAddress = 0;

		while (currentAddress < 8192) {
			let screen[currentAddress] = 0;
			let currentAddress = currentAddress + 1;
		}

		return;
	}

	function void setColor(boolean b) {
		let color = b;
		return;
	}

	/**
	 * The screen:
	 *  has 256 rows (0, 255), 512 columns (0, 511): 131072 pixels
	 *  is memory mapped as 8192 16-bit registers
	 */
	function void drawPixel(int x, int y) {
		var int addr, value, bit;

		/* a % x == a & (~x) if x is a power of 2
		 * Also, ~0b1000 == 0b0111 == 0b1000 - 1, hence x & 15 */
		let bit = x & 15;

		/* [screen + (y * 32) + (x / 16)] is too slow to compute on the fly */
		let addr = ys[y] + xs[x];

		if (color) {
			let value = Memory.peek(addr) | squaresUpToSixteen[bit];
		} else {
			/* Jack is weakly typed. true == 0b1111_1111_1111_1111 */
			let value = Memory.peek(addr) & (true - squaresUpToSixteen[bit]);
		}

		do Memory.poke(addr, value);
		return;
	}

	/**
	 * The algorithm zigzags to the coordinates when neither x1 == x2
	 * nor y1 == y2: in these cases, a straight line can be drawn.
	 */
	function void drawLine(int x1, int y1, int x2, int y2) {
		var int x, y;
		var int dx, dy, xProgress, yProgress, diff, xLimit, yLimit;
		var boolean yGoesUp, xGoesLeft;

		let x = x1;
		let y = y1;

		if (x1 > x2) { let xGoesLeft = true; } else { let xGoesLeft = false; }
		if (y1 > y2) { let yGoesUp = true; } else { let yGoesUp = false; }

		/* straight vertical line */
		if (x1 = x2) {
			if (yGoesUp) {
				while (y > y2) {
					do Screen.drawPixel(x, y);
					let y = y - 1;
				}
			} else {
				while (y < y2) {
					do Screen.drawPixel(x, y);
					let y = y + 1;
				}
			}

			return;
		}

		/* straight horizontal line */
		if (y1 = y2) {
			if (xGoesLeft) {
				while (x > x2) {
					do Screen.drawPixel(x, y);
					let x = x - 1;
				}
			} else {
				while (x < x2) {
					do Screen.drawPixel(x, y);
					let x = x + 1;
				}
			}

			return;
		}

		let diff = 0;
		let dx = Math.abs(x2 - x1);
		let dy = Math.abs(y2 - y1);

		let xProgress = 0;
		let yProgress = 0;
		let xLimit = dx + 1;
		let yLimit = dy + 1;

		if (dx > dy) {
			while (xProgress < xLimit) {
				if (yGoesUp) {
					if (xGoesLeft) {
						do Screen.drawPixel(x - xProgress, y - yProgress);
					} else {
						do Screen.drawPixel(x + xProgress, y - yProgress);
					}
				} else {
					if (xGoesLeft) {
						do Screen.drawPixel(x - xProgress, y + yProgress);
					} else {
						do Screen.drawPixel(x + xProgress, y + yProgress);
					}
				}

				if (diff < 0) {
					let xProgress = xProgress + 1;
					let diff = diff + dy;
				} else {
					let yProgress = yProgress + 1;
					let diff = diff - dx;
				}
			}
		} else {
			while (yProgress < yLimit) {
				if (yGoesUp) {
					if (xGoesLeft) {
						do Screen.drawPixel(x - xProgress, y - yProgress);
					} else {
						do Screen.drawPixel(x + xProgress, y - yProgress);
					}
				} else {
					if (xGoesLeft) {
						do Screen.drawPixel(x - xProgress, y + yProgress);
					} else {
						do Screen.drawPixel(x + xProgress, y + yProgress);
					}
				}

				if (diff < 0) {
					let xProgress = xProgress + 1;
					let diff = diff + dy;
				} else {
					let yProgress = yProgress + 1;
					let diff = diff - dx;
				}
			}
		}

		return;
	}

	function void drawRectangle(int x1, int y1, int x2, int y2) {
		var int nextY;
		let nextY = y1 + 1;

		do Screen.drawLine(x1, y1, x2, y1);
		while (nextY < y2) {
			do Screen.drawLine(x1, nextY, x2, nextY);
			let nextY = nextY + 1;
		}

		return;
	}

	function void drawCircle(int x, int y, int r) {
		var int dy, distance, limit, yCoord;

		let dy = -r;
		let limit = r + 1;

		while (dy < limit) {
			let yCoord = y + dy;
			let distance = Math.sqrt((r * r) - (dy * dy));
			do Screen.drawLine(x - distance, yCoord, x + distance, yCoord);
			let dy = dy + 1;
		}

		return;
	}
}
